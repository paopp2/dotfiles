# Aliases
# alias h="sudo systemctl hibernate"
alias shutdown="sudo shutdown -h now"
alias copy="xclip -selection clipboard"
alias x=exit
alias vim="nvim"
alias vimd='nvim -c "autocmd User VeryLazy PeekOpen"'
alias v="nvim"
alias vw="nvim -u $HOME/.config/vscode_nvim/init.vim -c VimwikiIndex"
alias untar="tar -xvzf"
alias adb="$HOME/Android/Sdk/platform-tools/adb"
alias sp="speedtest-cli --no-upload"
alias go="/usr/local/go/bin/go"
alias docker="sudo docker"
alias config='/usr/bin/git --git-dir=$HOME/.dotfiles/ --work-tree=$HOME'
alias ll='ls -lah'
alias h='print -zr -- $(history -rn 0 | fzf --reverse --info=inline)'
alias gdu='gdu-go --si'
alias c="caffeinate -d"
alias f="fzf"
alias d="docker"
alias dc="docker-compose"
alias awslocal="aws --endpoint-url=http://localhost:4566"
alias code="zed" # Fuck VSCode and Cursor. We're now using Zed. This is the future

# "r" = yazi; cd on exit
function r {
	local tmp="$(mktemp -t "yazi-cwd.XXXXXX")"
	yazi "$@" --cwd-file="$tmp"
	if cwd="$(cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
		cd -- "$cwd"
	fi
	rm -f -- "$tmp"
}

# Git Aliases
alias gs="git status"
alias ga="git add"
alias gcm="git commit"
alias gb="git branch --sort=-committerdate --format='%(HEAD) %(color:yellow)%(refname:short)%(color:reset) - %(color:cyan)%(authorname)%(color:reset) %(color:dim white)(%(committerdate:relative))%(color:reset)'"
alias gacm="git add .;git commit"
alias gl="git log"
alias gll='git log --pretty="%C(auto)%h %C(auto)%d%Creset%n    %C(cyan)%an %C(dim white)%ad%n    %s%Creset%n" --date=format:"%a %Y-%m-%d %H:%M"  --graph'
alias glla='git log --full-history --pretty="%C(auto)%h %C(auto)%d%Creset%n    %C(cyan)%an %C(dim white)%ad%n    %s%Creset%n" --date=format:"%a %Y-%m-%d %H:%M"  --date-order --skip=0 --branches --tags --remotes --graph'
alias gpul="git pull"
alias gp="git pull"
alias gpsh="git push"
alias gsh="git stash"
alias gshp="git stash pop"
alias gfa="git fetch --all"

# grba - rebase all branches in linear chain onto parent
function grba {
  local parent="${1:?Usage: grba <parent-branch>}"
  local current=$(git rev-parse --abbrev-ref HEAD)
  local branches=()
  local merge_base=$(git merge-base "$parent" "$current")

  # Find branches between merge-base and HEAD, sorted by distance
  while IFS= read -r branch; do
    [[ "$branch" == "$parent" ]] && continue
    if git merge-base --is-ancestor "$branch" "$current" && \
       git merge-base --is-ancestor "$merge_base" "$branch"; then
      branches+=("$(git rev-list --count "$merge_base".."$branch"):$branch")
    fi
  done < <(git branch --format='%(refname:short)')

  IFS=$'\n' branches=($(sort -t: -k1 -n <<<"${branches[*]}")); unset IFS

  local prev="$parent"
  for entry in "${branches[@]}"; do
    local branch="${entry#*:}"
    echo "Rebasing $branch onto $prev..."
    git checkout "$branch" && git rebase "$prev" || return 1
    if git rev-parse --abbrev-ref "$branch@{u}" &>/dev/null; then
      git push --force-with-lease
    fi
    prev="$branch"
  done

  echo "Done. Rebased ${#branches[@]} branch(es)."
}

# Github Aliases
alias pr="gh pr view -w"
alias prs="gh pr list -w"
alias prs-toreview='gh pr list --search "is:pr is:open -review:approved review-requested:@me" -w'
alias prs-me='gh pr list --author "@me"'

# Zed
alias zed="zed-preview"

# Diff two commits/branches in Zed's native git diff view
# Usage: zdf <hash>          → show changes in that commit
#        zdf <ref1> <ref2>   → diff between two refs
#        zdf                 → fzf picker (select two commits)
zdf() {
  local ref1 ref2
  if [[ $# -eq 0 ]]; then
    local git_output
    git_output=$(git log --graph --oneline --decorate --color=always \
      --full-history --date-order --branches --tags --remotes)

    # First pick
    local pick1
    pick1=$(echo "$git_output" | fzf --ansi --reverse \
             --header="Select first commit (Enter)")
    [[ -z "$pick1" ]] && return 0
    local hash1=$(echo "$pick1" | sed 's/^[^a-f0-9]*//' | awk '{print $1}')
    local desc1=$(git log -1 --format='%h %s' "$hash1" 2>/dev/null)

    # Find line number of first pick to restore cursor position
    local line_num
    line_num=$(echo "$git_output" | sed $'s/\033\\[[0-9;]*m//g' | grep -n "$hash1" | head -1 | cut -d: -f1)

    # Second pick, cursor at same position, highlight first pick in green
    local pick2
    pick2=$(echo "$git_output" \
      | awk -v h="$hash1" 'BEGIN{bg="\033[48;2;20;50;20m"}{
          s=$0; gsub(/\033\[[0-9;]*m/,"",s)
          if(s~h){line=$0; gsub(/\033\[0?m/,"&"bg,line); printf "%s%s\033[0m\n",bg,line}
          else print
        }' \
      | fzf --sync --ansi --reverse \
             --bind "load:pos(${line_num:-1})" \
             --header="1st: ${desc1}  —  Select second commit (same = view commit)")
    [[ -z "$pick2" ]] && return 0
    local hash2=$(echo "$pick2" | sed 's/^[^a-f0-9]*//' | awk '{print $1}')

    if [[ "$hash1" == "$hash2" ]]; then
      ref1="${hash1}~1"; ref2="${hash1}"
    else
      ref1="$hash1"; ref2="$hash2"
    fi
  elif [[ $# -eq 1 ]]; then
    ref1="$1~1"; ref2="$1"
  else
    ref1="$1"; ref2="$2"
  fi

  # Ensure older commit is ref1 (left), newer is ref2 (right)
  local ts1=$(git log -1 --format=%ct "$ref1" 2>/dev/null)
  local ts2=$(git log -1 --format=%ct "$ref2" 2>/dev/null)
  if [[ -n "$ts1" && -n "$ts2" && $ts1 -gt $ts2 ]]; then
    local swap="$ref1"; ref1="$ref2"; ref2="$swap"
  fi

  # Clean up previous zdiff worktrees
  for wt in /tmp/zdiff-*(/N); do
    git worktree remove "$wt" --force 2>/dev/null
    rm -rf "$wt" 2>/dev/null
  done
  git worktree prune 2>/dev/null

  local tmp=$(mktemp -d /tmp/zdiff-XXXXXXXX)

  git worktree add --detach "$tmp" "$ref1" 2>/dev/null
  git -C "$tmp" diff "$ref1" "$ref2" | git -C "$tmp" apply --allow-empty
  git -C "$tmp" add -N . 2>/dev/null
  chmod -R a-w "$tmp"

  # Toggle Ghostty, open Zed, then open git panel
  osascript -e 'tell application "System Events" to key code 36 using command down' &
  zed-preview "$tmp"
  (osascript -e '
    tell application "Zed Preview" to activate
    tell application "System Events" to key code 5 using {option down, shift down}
  ') &>/dev/null &
}

# Claude Code
alias cc="claude"
